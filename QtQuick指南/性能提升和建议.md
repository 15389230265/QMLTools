# Performance Considerations And Suggestions



## Timing Considerations

作为应用程序开发人员，您必须努力允许呈现引擎实现一致的60帧/秒刷新率。60 FPS意味着每帧之间大约有16毫秒可以进行处理，其中包括将绘图基元上传到图形硬件所需的处理。

实际上，这意味着应用程序开发人员应该：

- 尽可能使用异步，事件驱动编程
- 使用工作线程进行重要处理
- 永远不要手动旋转事件循环
- 在阻塞函数中，每帧的花费不会超过几毫秒

如果不这样做将导致跳帧，这会对用户体验产生严重影响。

**注意：**一个诱人但*永远不*应该使用的模式是创建自己的[QEventLoop](https://doc.qt.io/qt-5/qeventloop.html)或调用[QCoreApplication :: processEvents](https://doc.qt.io/qt-5/qcoreapplication.html#processEvents)()以避免在从QML调用的C ++代码块中阻塞。这很危险，因为当在信号处理程序或绑定中输入事件循环时，QML引擎继续运行其他绑定，动画，转换等。然后这些绑定会导致副作用，例如，破坏包含您的层次结构事件循环。



## Profiling

最重要的提示是：使用Qt Creator附带的QML分析器。了解应用程序在何处花费时间将使您能够专注于实际存在的问题区域，而不是可能存在的问题区域。有关如何使用QML分析工具的更多信息，请参阅Qt Creator手册。

确定最常运行的绑定，或者应用程序花费最多时间的功能，将允许您决定是否需要优化问题区域，或重新设计应用程序的某些实现细节，以便提高性能。尝试在不进行分析的情况下优化代码可能会导致非常小的而不是显着的性能改进。



## JavaScript Code

大多数QML应用程序将以动态函数，信号处理程序和属性绑定表达式的形式包含大量JavaScript代码。这通常不是问题。由于QML引擎中的一些优化，例如对绑定编译器所做的那些优化，它可以（在某些用例中）比调用C ++函数更快。但是，必须注意确保不会意外触发不必要的处理。



### Bindings

QML中有两种类型的绑定：优化绑定和非优化绑定。保持绑定表达式尽可能简单是一个好主意，因为QML引擎使用优化的绑定表达式求值程序，它可以评估简单的绑定表达式，而无需切换到完整的JavaScript执行环境。与更复杂（非优化）的绑定相比，这些优化的绑定的评估效率更高。优化绑定的基本要求是必须在编译时知道所访问的每个符号的类型信息。

绑定表达式时要避免的事情以最大化可优化性：

- 声明中间JavaScript变量
- 访问“var”属性
- 调用JavaScript函数
- 构造闭包或在绑定表达式中定义函数
- 访问直接评估范围之外的属性
- 写作其他属性作为副作用

当绑定知道它们正在使用的对象类型和属性时，它们是最快的。这意味着在某些情况下，绑定表达式中的非最终属性查找可能会更慢，其中可能正在查找的属性的类型已更改（例如，通过派生类型）。

立即评估范围可以概括为它包含：

- 表达式作用域对象的属性（对于绑定表达式，这是属性绑定所属的对象）
- 组件中任何对象的ID
- 组件中根项的属性

来自其他组件的对象和任何此类对象的属性，以及JavaScript导入中定义或包含的符号都不在直接评估范围内，因此不会优化访问任何这些对象的绑定。

请注意，如果QML引擎的优化绑定表达式求值程序无法优化绑定，因此必须通过完整的JavaScript环境进行评估，则上面列出的一些提示将不再适用。例如，在非常复杂的绑定中将属性解析的结果缓存在中间JavaScript变量中有时是有益的。即将到来的部分提供了有关这些优化的更多信息。



### Type-Conversion

使用JavaScript的一个主要成本是，在大多数情况下，当访问QML类型的属性时，会创建一个包含底层C ++数据（或对它的引用）的外部资源的JavaScript对象。在大多数情况下，这是相当便宜的，但在其他情况下，它可能相当昂贵。昂贵的一个例子是将C ++ [QVariantMap ](https://doc.qt.io/qt-5/qvariant.html#QVariantMap-typedef)[Q_PROPERTY](https://doc.qt.io/qt-5/qobject.html#Q_PROPERTY)分配给QML“variant”属性。列表也可能是昂贵的，尽管（特定类型的序列[的QList](https://doc.qt.io/qt-5/qlist.html)为int，QREAL，布尔，[QString的](https://doc.qt.io/qt-5/qstring.html)，和[QUrl](https://doc.qt.io/qt-5/qurl.html)）应该便宜; 其他列表类型涉及昂贵的转换成本（创建新的JavaScript数组，逐个添加新类型，从C ++类型实例到JavaScript值的每类型转换）。

在一些基本属性类型（例如“string”和“url”属性）之间进行转换也可能很昂贵。使用最接近的匹配属性类型将避免不必要的转换。

如果必须将[QVariantMap](https://doc.qt.io/qt-5/qvariant.html#QVariantMap-typedef)公开给QML，请使用“var”属性而不是“variant”属性。一般来说，对于来自[QtQuick](https://doc.qt.io/qt-5/qtquick-qmlmodule.html) 2.0及更新版本的每个用例，“property var”应该被认为优于“属性变体” （注意“属性变体”被标记为过时），因为它允许真正的JavaScript引用存储（可以减少某些表达式中所需的转换次数）。



### Resolving Properties

财产解决需要时间。虽然在某些情况下可以缓存和重用查找结果，但如果可能的话，最好完全避免完成不必要的工作。

在下面的示例中，我们有一个经常运行的代码块（在这种情况下，它是显式循环的内容;但它可能是一个通常评估的绑定表达式，例如），在其中，我们解决了具有“rect”id及其“color”属性的对象多次：

```
// bad.qml
import QtQuick 2.3

Item {
    width: 400
    height: 200
    Rectangle {
        id: rect
        anchors.fill: parent
        color: "blue"
    }

    function printValue(which, value) {
        console.log(which + " = " + value);
    }

    Component.onCompleted: {
        var t0 = new Date();
        for (var i = 0; i < 1000; ++i) {
            printValue("red", rect.color.r);
            printValue("green", rect.color.g);
            printValue("blue", rect.color.b);
            printValue("alpha", rect.color.a);
        }
        var t1 = new Date();
        console.log("Took: " + (t1.valueOf() - t0.valueOf()) + " milliseconds for 1000 iterations");
    }
}
```

我们可以在块中只解析一次公共基数：

```
// good.qml
import QtQuick 2.3

Item {
    width: 400
    height: 200
    Rectangle {
        id: rect
        anchors.fill: parent
        color: "blue"
    }

    function printValue(which, value) {
        console.log(which + " = " + value);
    }

    Component.onCompleted: {
        var t0 = new Date();
        for (var i = 0; i < 1000; ++i) {
            var rectColor = rect.color; // resolve the common base.
            printValue("red", rectColor.r);
            printValue("green", rectColor.g);
            printValue("blue", rectColor.b);
            printValue("alpha", rectColor.a);
        }
        var t1 = new Date();
        console.log("Took: " + (t1.valueOf() - t0.valueOf()) + " milliseconds for 1000 iterations");
    }
}
```

只需这一简单的改变就可以显着提高性能。请注意，上面的代码可以进一步改进（因为在循环处理期间查找的属性永远不会改变），通过将属性解析提升出循环，如下所示：

```
// better.qml
import QtQuick 2.3

Item {
    width: 400
    height: 200
    Rectangle {
        id: rect
        anchors.fill: parent
        color: "blue"
    }

    function printValue(which, value) {
        console.log(which + " = " + value);
    }

    Component.onCompleted: {
        var t0 = new Date();
        var rectColor = rect.color; // resolve the common base outside the tight loop.
        for (var i = 0; i < 1000; ++i) {
            printValue("red", rectColor.r);
            printValue("green", rectColor.g);
            printValue("blue", rectColor.b);
            printValue("alpha", rectColor.a);
        }
        var t1 = new Date();
        console.log("Took: " + (t1.valueOf() - t0.valueOf()) + " milliseconds for 1000 iterations");
    }
}
```



### Property Bindings

如果更改了引用的任何属性，则将重新评估属性绑定表达式。因此，绑定表达式应尽可能简单。

如果你有一个循环来进行某些处理，但只有处理的最终结果很重要，通常最好更新一个临时累加器，然后将其分配给需要更新的属性，而不是逐步更新属性本身，以避免在积累的中间阶段触发重新评估结合表达。

以下设计的例子说明了这一点：

```
// bad.qml
import QtQuick 2.3

Item {
    id: root
    width: 200
    height: 200
    property int accumulatedValue: 0

    Text {
        anchors.fill: parent
        text: root.accumulatedValue.toString()
        onTextChanged: console.log("text binding re-evaluated")
    }

    Component.onCompleted: {
        var someData = [ 1, 2, 3, 4, 5, 20 ];
        for (var i = 0; i < someData.length; ++i) {
            accumulatedValue = accumulatedValue + someData[i];
        }
    }
}
```

onCompleted处理程序中的循环导致“text”属性绑定被重新评估六次（然后导致依赖于文本值的任何其他属性绑定，以及onTextChanged信号处理程序，每次重新评估时间，并列出每次显示的文本）。在这种情况下，这显然是不必要的，因为我们真的只关心积累的最终价值。

它可以改写如下：

```
// good.qml
import QtQuick 2.3

Item {
    id: root
    width: 200
    height: 200
    property int accumulatedValue: 0

    Text {
        anchors.fill: parent
        text: root.accumulatedValue.toString()
        onTextChanged: console.log("text binding re-evaluated")
    }

    Component.onCompleted: {
        var someData = [ 1, 2, 3, 4, 5, 20 ];
        var temp = accumulatedValue;
        for (var i = 0; i < someData.length; ++i) {
            temp = temp + someData[i];
        }
        accumulatedValue = temp;
    }
}
```



### Sequence tips

如前所述，某些序列类型很快（例如，[QList](https://doc.qt.io/qt-5/qlist.html) < int >，[QList](https://doc.qt.io/qt-5/qlist.html) < qreal >，[QList](https://doc.qt.io/qt-5/qlist.html) < bool >，[QList](https://doc.qt.io/qt-5/qlist.html) < [QString](https://doc.qt.io/qt-5/qstring.html) >，[QStringList](https://doc.qt.io/qt-5/qstringlist.html)和[QList](https://doc.qt.io/qt-5/qlist.html) < [QUrl](https://doc.qt.io/qt-5/qurl.html)>），而其他序列类型则要慢得多。除了尽可能使用这些类型而不是较慢的类型之外，还需要注意一些其他与性能相关的语义以获得最佳性能。

首先，对于序列类型的两种不同的实现：一个是当序列是[Q_PROPERTY](https://doc.qt.io/qt-5/qobject.html#Q_PROPERTY)一个的[QObject的](https://doc.qt.io/qt-5/qobject.html)（我们称此为参考序列），另一个用于在序列从返回[Q_INVOKABLE](https://doc.qt.io/qt-5/qobject.html#Q_INVOKABLE)一个功能[的QObject](https://doc.qt.io/qt-5/qobject.html)（我们将这称为复制序列。

通过[QMetaObject :: property](https://doc.qt.io/qt-5/qmetaobject.html#property)()读取和写入引用序列，从而读取和写入[QVariant](https://doc.qt.io/qt-5/qvariant.html)。这意味着从JavaScript更改序列中任何元素的值将导致三个步骤发生：将从[QObject](https://doc.qt.io/qt-5/qobject.html)读取完整序列（作为[QVariant](https://doc.qt.io/qt-5/qvariant.html)，但随后转换为正确类型的序列）; 指定索引处的元素将在该序列中更改; 并且完整的序列将被写回[QObject](https://doc.qt.io/qt-5/qobject.html)（作为[QVariant](https://doc.qt.io/qt-5/qvariant.html)）。

复制序列更简单，因为实际序列存储在JavaScript对象的资源数据中，因此不会发生读取/修改/写入循环（而是直接修改资源数据）。

因此，对参考序列的元素的写入将比写入复制序列的元素慢得多。实际上，写入N元素参考序列的单个元素与将N元素复制序列分配给该参考序列的成本相当，因此通常最好修改临时复制序列，然后将结果分配给计算过程中的参考序列。

假设以下C ++类型存在（以及之前注册到“Qt.example 1.0”命名空间）：

```c++
class SequenceTypeExample : public QQuickItem
{
    Q_OBJECT
    Q_PROPERTY (QList<qreal> qrealListProperty READ qrealListProperty WRITE setQrealListProperty NOTIFY qrealListPropertyChanged)

public:
    SequenceTypeExample() : QQuickItem() { m_list << 1.1 << 2.2 << 3.3; }
    ~SequenceTypeExample() {}

    QList<qreal> qrealListProperty() const { return m_list; }
    void setQrealListProperty(const QList<qreal> &list) { m_list = list; emit qrealListPropertyChanged(); }

signals:
    void qrealListPropertyChanged();

private:
    QList<qreal> m_list;
};
```

以下示例在紧密循环中写入引用序列的元素，从而导致性能下降：

```
// bad.qml
import QtQuick 2.3
import Qt.example 1.0

SequenceTypeExample {
    id: root
    width: 200
    height: 200

    Component.onCompleted: {
        var t0 = new Date();
        qrealListProperty.length = 100;
        for (var i = 0; i < 500; ++i) {
            for (var j = 0; j < 100; ++j) {
                qrealListProperty[j] = j;
            }
        }
        var t1 = new Date();
        console.log("elapsed: " + (t1.valueOf() - t0.valueOf()) + " milliseconds");
    }
}
```

由表达式引起的内部循环中的[QObject](https://doc.qt.io/qt-5/qobject.html)属性读取和写入`"qrealListProperty[j] = j"`使得此代码非常不理想。相反，功能相当但更快的东西是：

```
// good.qml
import QtQuick 2.3
import Qt.example 1.0

SequenceTypeExample {
    id: root
    width: 200
    height: 200

    Component.onCompleted: {
        var t0 = new Date();
        var someData = [1.1, 2.2, 3.3]
        someData.length = 100;
        for (var i = 0; i < 500; ++i) {
            for (var j = 0; j < 100; ++j) {
                someData[j] = j;
            }
            qrealListProperty = someData;
        }
        var t1 = new Date();
        console.log("elapsed: " + (t1.valueOf() - t0.valueOf()) + " milliseconds");
    }
}
```

其次，如果属性中的任何元素发生变化，则会发出属性的更改信号。如果你对序列属性中的特定元素有很多绑定，最好创建一个绑定到该元素的动态属性，并将该动态属性用作绑定表达式中的符号而不是sequence元素，因为它将只有在其值发生变化时才会重新评估绑定。

这是一个不寻常的用例，大多数客户都不应该打，但值得注意，以防你发现自己做了这样的事情：

```
// bad.qml
import QtQuick 2.3
import Qt.example 1.0

SequenceTypeExample {
    id: root

    property int firstBinding: qrealListProperty[1] + 10;
    property int secondBinding: qrealListProperty[1] + 20;
    property int thirdBinding: qrealListProperty[1] + 30;

    Component.onCompleted: {
        var t0 = new Date();
        for (var i = 0; i < 1000; ++i) {
            qrealListProperty[2] = i;
        }
        var t1 = new Date();
        console.log("elapsed: " + (t1.valueOf() - t0.valueOf()) + " milliseconds");
    }
}
```

请注意，即使在循环中仅修改索引2处的元素，也会重新评估三个绑定，因为更改信号的粒度是整个属性已更改。因此，添加中间绑定有时可能是有益的：

```
// good.qml
import QtQuick 2.3
import Qt.example 1.0

SequenceTypeExample {
    id: root

    property int intermediateBinding: qrealListProperty[1]
    property int firstBinding: intermediateBinding + 10;
    property int secondBinding: intermediateBinding + 20;
    property int thirdBinding: intermediateBinding + 30;

    Component.onCompleted: {
        var t0 = new Date();
        for (var i = 0; i < 1000; ++i) {
            qrealListProperty[2] = i;
        }
        var t1 = new Date();
        console.log("elapsed: " + (t1.valueOf() - t0.valueOf()) + " milliseconds");
    }
}
```

在上面的示例中，每次仅重新评估中间绑定，从而导致显着的性能提升。



### Value-Type tips

值类型属性（font，color，vector3d等）具有类似的[QObject](https://doc.qt.io/qt-5/qobject.html)属性，并将通知语义更改为序列类型属性。因此，上面给出的序列提示也适用于值类型属性。虽然它们通常不是值类型的问题（因为值类型的子属性的数量通常远小于序列中元素的数量），所以重新评估的绑定数量的任何增加不必要地会对绩效产生负面影响。



### Other JavaScript Objects

不同的JavaScript引擎提供不同的优化。[Qt Quick 2](https://doc.qt.io/qt-5/qtquick-index.html)使用的JavaScript引擎针对对象实例化和属性查找进行了优化，但它提供的优化依赖于某些标准。如果您的应用程序不符合标准，则JavaScript引擎会回退到“慢速路径”模式，性能会更差。因此，请始终尽量确保您符合以下条件：

- 尽可能避免使用eval()
- 不要删除对象的属性



## Common Interface Elements



### Text Elements

计算文本布局可能是一个缓慢的操作。考虑使用`PlainText`格式而不是`StyledText`尽可能使用格式，因为这会减少布局引擎所需的工作量。如果您不能使用`PlainText`（因为您需要嵌入图像，或使用标签指定字符范围以具有某些格式（粗体，斜体等）而不是整个文本），那么您应该使用`StyledText`。

您应该只`AutoText`在文本可能（但可能不是）时使用，`StyledText`因为此模式将导致解析成本。`RichText`不应该使用该模式，因为`StyledText`它提供了几乎所有功能，而成本只是其一小部分。



### Images

图像是任何用户界面的重要组成部分。不幸的是，由于加载它们所花费的时间，它们消耗的内存量以及它们的使用方式，它们也是一个很大的问题来源。



#### Asynchronous Loading

图像通常非常大，因此确保加载图像不会阻止UI线程是明智的。将QML Image元素的“异步”属性设置`true`为启用从本地文件系统异步加载图像（远程图像始终异步加载），这不会对用户界面的美观产生负面影响。

设置为“异步”属性的图像元素`true`将在低优先级工作线程中加载图像。



#### Explicit Source Size

如果应用程序加载大图像但是将其显示在一个小尺寸元素中，请将“sourceSize”属性设置为要渲染的元素的大小，以确保图像的较小比例版本保留在内存中，而不是大的。

请注意，更改sourceSize将导致重新加载图像。



#### Avoid Run-time Composition

还要记住，通过在应用程序中提供预先组合的图像资源（例如，提供带阴影效果的元素），可以避免在运行时进行合成工作。



#### Avoid Smoothing Images

`image.smooth`仅在需要时启用。它在某些硬件上较慢，如果图像以其自然大小显示，则没有视觉效果。



#### Painting

避免多次涂抹同一区域。使用Item作为根元素而不是Rectangle以避免多次绘制背景。



### Position Elements With Anchors

使用锚点而不是绑定到相对于彼此的位置项更有效。考虑使用绑定来相对于rect1定位rect2：

```
Rectangle {
    id: rect1
    x: 20
    width: 200; height: 200
}
Rectangle {
    id: rect2
    x: rect1.x
    y: rect1.y + rect1.height
    width: rect1.width - 20
    height: 200
}
```

使用锚点可以更有效地实现这一目标：

```
Rectangle {
    id: rect1
    x: 20
    width: 200; height: 200
}
Rectangle {
    id: rect2
    height: 200
    anchors.left: rect1.left
    anchors.top: rect1.bottom
    anchors.right: rect1.right
    anchors.rightMargin: 20
}
```

使用绑定定位（通过将绑定表达式分配给可视对象的x，y，width和height属性，而不是使用锚点）相对较慢，尽管它允许最大的灵活性。

如果布局不是动态的，则指定布局的最佳方式是通过x，y，width和height属性的静态初始化。项目坐标始终相对于其父项，因此如果您想要与父项的0,0坐标成固定偏移，则不应使用锚点。在下面的示例中，子Rectangle对象位于相同的位置，但是显示的锚点代码不像通过静态初始化使用固定定位的代码那样具有资源效率：

```
Rectangle {
    width: 60
    height: 60
    Rectangle {
        id: fixedPositioning
        x: 20
        y: 20
        width: 20
        height: 20
    }
    Rectangle {
        id: anchorPositioning
        anchors.fill: parent
        anchors.margins: 20
    }
}
```



## Models and Views

大多数应用程序将至少有一个模型将数据提供给视图。应用程序开发人员需要注意一些语义，以实现最大性能。



### Custom C++ Models

通常需要在C ++中编写自己的自定义模型，以便与QML中的视图一起使用。虽然任何此类模型的最佳实现在很大程度上取决于它必须满足的用例，但一些一般指导原则如下：

- 尽量保持异步
- 在（低优先级）工作线程中进行所有处理
- 批量处理后端操作，以便（可能很慢）I / O和IPC最小化
- 使用滑动切片窗口来缓存结果，其参数在分析的帮助下确定

重要的是要注意，建议使用低优先级的工作线程，以最大限度地降低GUI线程饥饿的风险（这可能导致更差的感知性能）。此外，请记住，同步和锁定机制可能是性能降低的重要原因，因此应注意避免不必要的锁定。



### ListModel QML Type

QML提供了一个[ListModel](https://doc.qt.io/qt-5/qml-qtqml-models-listmodel.html)类型，可用于将数据提供给[ListView](https://doc.qt.io/qt-5/qml-qtquick-listview.html)。它应该足以满足大多数用例，并且只要正确使用它就能保持相对性能。



#### Populate Within A Worker Thread

[ListModel](https://doc.qt.io/qt-5/qml-qtqml-models-listmodel.html)元素可以在JavaScript中的（低优先级）工作线程中填充。开发人员必须在[WorkerScript中](https://doc.qt.io/qt-5/workerscript-qmlmodule.html)从[ListModel](https://doc.qt.io/qt-5/qml-qtqml-models-listmodel.html)显式调用“sync()”，以使更改与主线程同步。有关更多信息，请参阅[WorkerScript](https://doc.qt.io/qt-5/workerscript-qmlmodule.html)文档。

请注意，使用[WorkerScript](https://doc.qt.io/qt-5/workerscript-qmlmodule.html)元素将导致创建单独的JavaScript引擎（因为JavaScript引擎是每个线程）。这将导致内存使用量增加。但是，多个[WorkerScript](https://doc.qt.io/qt-5/workerscript-qmlmodule.html)元素都将使用相同的工作线程，因此一旦应用程序使用了一个，则使用第二个或第三个[WorkerScript](https://doc.qt.io/qt-5/workerscript-qmlmodule.html)元素的内存影响可以忽略不计。



#### Don't Use Dynamic Roles

[QtQuick](https://doc.qt.io/qt-5/qtquick-qmlmodule.html) 2中的[ListModel](https://doc.qt.io/qt-5/qml-qtqml-models-listmodel.html)元素比[QtQuick](https://doc.qt.io/qt-5/qtquick-qmlmodule.html) 1 更[高效](https://doc.qt.io/qt-5/qtquick-qmlmodule.html)。性能改进主要来自对给定模型中每个元素内角色类型的假设 - 如果类型没有改变，缓存性能会大大提高。如果类型可以在元素之间动态地改变，则这种优化变得不可能，并且模型的性能将变差一个数量级。

因此，默认情况下禁用动态类型; 开发人员必须专门设置模型的布尔“dynamicRoles”属性以启用动态类型（并伴随着性能降低）。如果可以重新设计您的应用程序以避免它，我们建议您不要使用动态类型。



### Views

视图代表应尽可能简单。在委托中有足够的QML来显示必要的信息。在需要之前，不应创建任何非立即需要的附加功能（例如，如果在单击时显示更多信息）（请参阅即将进行的延迟初始化部分）。

以下列表是设计委托时要记住的事项的一个很好的总结：

- 委托中的元素越少，创建的速度就越快，因此滚动视图的速度就越快。
- 将委托中的绑定数量保持在最低限度; 特别是，使用锚点而不是绑定来在委托中进行相对定位。
- 避免在委托中使用[ShaderEffect](https://doc.qt.io/qt-5/qml-qtquick-shadereffect.html)元素。
- 永远不要在委托上启用剪辑。

您可以设置`cacheBuffer`视图的属性，以允许异步创建和缓冲可见区域之外的委托。`cacheBuffer`对于非平凡且不太可能在单个帧中创建的视图委托，建议使用a 。

请记住, `cacheBuffer`将额外的代理保留在内存中。因此，利用它`cacheBuffer`得到的值必须与额外的内存使用量相平衡。开发人员应该使用基准测试来为他们的用例找到最佳值，因为`cacheBuffer`在一些罕见的情况下，使用can 引起的内存压力增加会导致滚动时帧速率降低。



## Visual Effects

[Qt Quick 2](https://doc.qt.io/qt-5/qtquick-index.html)包含一些功能，允许开发人员和设计人员创建极具吸引力的用户界面。流动性和动态转换以及视觉效果可以在应用程序中发挥很大作用，但在使用QML中的某些功能时必须要小心，因为它们可能会影响性能。



### Animations

通常，动画属性将导致重新评估引用该属性的任何绑定。通常，这是所期望的，但在其他情况下，在执行动画之前禁用绑定可能更好，然后在动画完成后重新分配绑定。

避免在动画期间运行JavaScript。例如，应避免为x属性动画的每个帧运行复杂的JavaScript表达式。

开发人员应特别小心使用脚本动画，因为它们在主线程中运行（因此如果完成时间过长会导致帧被跳过）。



### Particles

在[Qt的快速粒子](https://doc.qt.io/qt-5/qtquick-particles-qmlmodule.html)模块允许美丽粒子效果被无缝地集成到用户界面。但是，每个平台都具有不同的图形硬件功能，并且Particles模块无法将参数限制为硬件可以优雅支持的参数。您尝试渲染的粒子越多（它们越大），图形硬件需要的速度就越快，以60 FPS渲染。影响更多粒子需要更快的CPU。因此，仔细测试目标平台上的所有粒子效果，校准可以60 FPS渲染的粒子的数量和大小非常重要。

应当注意，可以在不使用时（例如，在不可见元素上）禁用粒子系统以避免进行不必要的模拟。

有关更深入的信息，请参阅“ [粒子系统性能指南”](https://doc.qt.io/qt-5/qtquick-particles-performance.html)。



## Controlling Element Lifetime

通过将应用程序划分为简单的模块化组件（每个组件包含在一个QML文件中），您可以实现更快的应用程序启动时间并更好地控制内存使用，并减少应用程序中活动但不可见元素的数量。



### Lazy Initialization

QML引擎做了一些棘手的事情，试图确保组件的加载和初始化不会导致跳过帧。但是，没有更好的方法来减少启动时间，而不是避免做你不需要做的工作，并且延迟工作直到必要。这可以通过使用[Loader](https://doc.qt.io/qt-5/qml-qtquick-loader.html)或[动态](https://doc.qt.io/qt-5/qtqml-javascript-dynamicobjectcreation.html)创建组件来实现。



#### Using Loader

Loader是一个允许动态加载和卸载组件的元素。

- 使用Loader的“active”属性，可以延迟初始化直到需要。
- 使用“setSource()”函数的重载版本，可以提供初始属性值。
- 将Loader [异步](https://doc.qt.io/qt-5/qml-qtquick-loader.html#asynchronous-prop)属性设置为true还可以在实例化组件时提高流动性。



#### Using Dynamic Creation

开发人员可以使用Qt.createComponent()函数在运行时从JavaScript中动态创建组件，然后调用createObject()来实例化它。根据调用中指定的所有权语义，开发人员可能必须手动删除创建的对象。有关详细信息，请参阅 [Dynamic QML Object Creation from JavaScript](https://doc.qt.io/qt-5/qtqml-javascript-dynamicobjectcreation.html) 



### Destroy Unused Elements

因为它们是不可见元素的子元素而不可见的元素（例如，选项卡小部件中的第二个选项卡，而显示第一个选项卡）在大多数情况下应该被懒惰地初始化，并且在不再使用时被删除，以避免使它们保持活动的持续成本（例如，渲染，动画，属性绑定评估等）。

可以通过重置Loader的“source”或“sourceComponent”属性来释放加载了Loader元素的项，而可以通过调用其上的destroy()来显式释放其他项。在某些情况下，可能需要使项目保持活动状态，在这种情况下，它至少应该是不可见的。

有关活动但不可见元素的更多信息，请参阅即将出现的关于渲染的部分。



## Rendering

用于在[QtQuick](https://doc.qt.io/qt-5/qtquick-qmlmodule.html) 2中渲染的场景图允许高动态，动画的用户界面以60 FPS流畅渲染。但是，有些事情会大大降低渲染性能，开发人员应该尽可能地避免这些陷阱。





### Clipping

默认情况下禁用剪切，并且只应在需要时启用剪辑。

剪辑是一种视觉效果，而不是优化。它增加（而不是降低）渲染器的复杂性。如果启用了剪裁，则项目会将其自己的绘画以及其子画面绘制到其边界矩形。这使得渲染器无法自由地重新排序元素的绘制顺序，从而导致次优的最佳情况场景图遍历。

在委托内部剪辑特别糟糕，应该不惜一切代价避免。



### Over-drawing and Invisible Elements

如果你有完全由其他（不透明）元素覆盖的元素，最好将它们的“可见”属性设置为`false`或者它们将被不必要地绘制。

类似地，不可见的元素（例如，选项卡小部件中的第二个选项卡，显示第一个选项卡）但需要在启动时初始化（例如，如果实例化第二个选项卡的成本花费太长时间只有在激活选项卡时才能执行此操作），应将其“可见”属性设置为`false`，以避免绘制它们的成本（尽管如前所述，它们仍然会产生任何动画或绑定评估的成本，因为他们仍然活跃）。



### Translucent vs Opaque

绘制的不透明内容通常比半透明快得多。原因是半透明内容需要混合，并且渲染器可以更好地优化不透明内容。

具有一个半透明像素的图像被视为完全半透明，即使它大部分是不透明的。具有透明边缘的[BorderImage](https://doc.qt.io/qt-5/qml-qtquick-borderimage.html)也是如此。



### Shaders

该[ShaderEffect](https://doc.qt.io/qt-5/qml-qtquick-shadereffect.html)类型能够把GLSL代码内嵌在Qt Quick的应用具有非常小的开销。但是，重要的是要意识到片段程序需要针对渲染形状中的每个像素运行。当部署到低端硬件并且着色器覆盖大量像素时，应该将片段着色器保留为几条指令以避免性能不佳。

用GLSL编写的着色器允许编写复杂的变换和视觉效果，但应谨慎使用。使用[ShaderEffectSource](https://doc.qt.io/qt-5/qml-qtquick-shadereffectsource.html)会导致场景在绘制之前预先渲染到FBO中。这额外的开销可能非常昂贵。



## Memory Allocation And Collection

应用程序将分配的内存量以及分配内存的方式是非常重要的考虑因素。除了对内存受限设备上的内存不足情况的明显担忧之外，在堆上分配内存是一项计算成本相当高的操作，并且某些分配策略可能导致跨页面的数据碎片增加。JavaScript使用自动垃圾收集的托管内存堆，这有一些优点，但也有一些重要的含义。

用QML编写的应用程序使用来自C ++堆和自动管理的JavaScript堆的内存。应用程序开发人员需要了解每个细节的微妙之处，以便最大限度地提高性能。



### Tips For QML Application Developers

本节中包含的提示和建议仅供参考，可能并不适用于所有情况。务必使用经验指标仔细评估和分析您的应用程序，以便做出最佳决策。



#### Instantiate and initialize components lazily

如果您的应用程序包含多个视图（例如，多个选项卡），但任何时候只需要一个，您可以使用延迟实例化来最小化在任何给定时间分配的内存量。有关更多信息，请参阅有关[延迟初始化](https://doc.qt.io/qt-5/qtquick-performance.html#lazy-initialization)的前一节。



#### Destroy unused objects

如果您在JavaScript表达式中延迟加载组件或动态创建对象，则通常`destroy()`手动更好，而不是等待自动垃圾收集这样做。有关更多信息，请参阅[控制元素生命周期](https://doc.qt.io/qt-5/qtquick-performance.html#controlling-element-lifetime)的前一节。



#### Don't manually invoke the garbage collector

在大多数情况下，手动调用垃圾收集器是不明智的，因为它会在很长一段时间内阻止GUI线程。这可能导致跳帧和动画不稳定，应该不惜一切代价避免。

在某些情况下，手动调用垃圾收集器是可以接受的（这将在下一节中详细解释），但在大多数情况下，调用垃圾收集器是不必要的并且适得其反。



#### Avoid complex bindings

除了复杂绑定的性能降低（例如，由于必须进入JavaScript执行上下文以执行评估），它们在C ++堆和JavaScript堆上也占用了比QML优化可以评估的绑定更多的内存。绑定表达式评估程序。



#### Avoid defining multiple identical implicit types

如果QML元素具有在QML中定义的自定义属性，则它将成为自己的隐式类型。这将在下一节中更详细地解释。如果在组件中内联定义了多个相同的隐式类型，则会浪费一些内存。在这种情况下，通常最好明确定义一个可以重用的新组件。

定义自定义属性通常可以是有益的性能优化（例如，减少所需或重新评估的绑定数），或者它可以提高组件的模块性和可维护性。在这些情况下，鼓励使用自定义属性。但是，如果多次使用新类型，则应将其拆分为自己的组件（.qml文件）以节省内存。



#### Reuse existing components

如果您正在考虑定义一个新组件，那么值得仔细检查这个组件在您的平台的组件集中是否已经存在。否则，您将强制QML引擎生成并存储类型的类型数据，该类型实质上是另一个预先存在且可能已加载的组件的副本。



#### Use singleton types instead of pragma library scripts

如果使用pragma库脚本来存储应用程序范围的实例数据，请考虑使用[QObject](https://doc.qt.io/qt-5/qobject.html)单例类型。这应该会带来更好的性能，并且会减少使用的JavaScript堆内存。



### Memory Allocation in a QML Application

QML应用程序的内存使用情况可能分为两部分：C ++堆使用情况和JavaScript堆使用情况。每个内存中分配的一些内存将是不可避免的，因为它是由QML引擎或JavaScript引擎分配的，而其余内存则取决于应用程序开发人员做出的决策。

C ++堆将包含：

- QML引擎的固定和不可避免的开销（实现数据结构，上下文信息等）;
- 每个组件的编译数据和类型信息，包括每类型属性元数据，它由QML引擎生成，具体取决于应用程序加载哪些模块和哪些组件;
- 每个对象的C ++数据（包括属性值）加上每个元素的元对象层次结构，具体取决于应用程序实例化的组件;
- 由QML导入（库）专门分配的任何数据。

JavaScript堆将包含：

- JavaScript引擎本身固定且不可避免的开销（包括内置的JavaScript类型）;
- 我们的JavaScript集成的固定和不可避免的开销（加载类型，函数模板等的构造函数）;
- JavaScript引擎在运行时为每种类型生成的每类型布局信息和其他内部类型数据（请参阅下面的注释，关于类型）;
- 每个对象的JavaScript数据（“var”属性，JavaScript函数和信号处理程序，以及非优化的绑定表达式）;
- 表达式评估期间分配的变量

此外，将分配一个JavaScript堆用于主线程，并且可选择另一个JavaScript堆分配用于[WorkerScript](https://doc.qt.io/qt-5/workerscript-qmlmodule.html)线程。如果应用程序不使用[WorkerScript](https://doc.qt.io/qt-5/workerscript-qmlmodule.html)元素，则不会产生该开销。JavaScript堆的大小可以是几兆字节，因此尽管在异步填充列表模型时有用，但是为了内存受限的设备编写的应用程序可以通过避免使用[WorkerScript](https://doc.qt.io/qt-5/workerscript-qmlmodule.html)元素来获得最佳效果。

请注意，QML引擎和JavaScript引擎都会自动生成自己的关于观察到的类型的类型数据缓存。应用程序加载的每个组件都是一个不同的（显式）类型，在QML中定义自己的自定义属性的每个元素（组件实例）都是一个隐式类型。JavaScript和QML引擎将任何未定义任何自定义属性的元素（组件实例）视为组件显式定义的类型，而不是其自己的隐式类型。

请考虑以下示例：

```
import QtQuick 2.3

Item {
    id: root

    Rectangle {
        id: r0
        color: "red"
    }

    Rectangle {
        id: r1
        color: "blue"
        width: 50
    }

    Rectangle {
        id: r2
        property int customProperty: 5
    }

    Rectangle {
        id: r3
        property string customProperty: "hello"
    }

    Rectangle {
        id: r4
        property string customProperty: "hello"
    }
}
```

在前面的示例中，矩形`r0`并`r1`没有任何自定义属性，因此JavaScript和QML引擎认为它们都属于同一类型。也就是说，`r0`并且`r1`都被认为是明确定义的`Rectangle`类型。矩形`r2`，`r3`并且`r4`每个都具有自定义属性和各自认为是不同的（隐式）的类型。需要注意的是`r3`，并`r4`分别被认为是不同类型的，即使他们有相同的属性信息，仅仅是因为自定义属性并没有在他们的情况下，该组件声明。

如果`r3`和`r4`是一个的两个实例`RectangleWithString`组成部分，该组件定义包括一个字符串属性的命名申报`customProperty`，然后`r3`和`r4`会被认为是同一类型的（也就是说，它们将是实例`RectangleWithString`类型，而不是定义自己的隐式）。



### In-Depth Memory Allocation Considerations

每当做出有关内存分配或性能权衡的决策时，请务必记住CPU缓存性能，操作系统分页和JavaScript引擎垃圾回收的影响。应仔细对潜在的解决方案进行基准测试，以确保选择最佳解决方案。

没有一套通用指南可以取代对计算机科学基本原理的深刻理解，并结合应用程序开发人员正在开发的平台的实现细节的实际知识。此外，在做出权衡决策时，没有多少理论计算可以取代一套好的基准和分析工具。



#### Fragmentation

碎片化是一个C ++开发问题。如果应用程序开发人员没有定义任何C ++类型或插件，他们可以安全地忽略此部分。

随着时间的推移，应用程序将分配大部分内存，将数据写入该内存，并在完成使用某些数据后随后释放部分内存。这可能导致“空闲”内存位于非连续的块中，这些块不能返回到操作系统以供其他应用程序使用。它还对应用程序的缓存和访问特性产生影响，因为“活”数据可能分布在物理内存的许多不同页面上。这反过来可能会迫使操作系统进行交换，这会导致文件系统I / O - 相对而言，这是一个非常慢的操作。

通过利用池分配器（和其他连续的内存分配器），通过仔细管理对象生命周期，通过定期清理和重建缓存，或通过利用内存管理的运行时，减少任何时间分配的内存量，可以避免碎片垃圾收集（如JavaScript）。



#### Garbage Collection

JavaScript提供垃圾收集。在JavaScript堆上分配的内存（与C ++堆相对）由JavaScript引擎拥有。引擎将定期收集JavaScript堆上的所有未引用数据。



##### Implications of Garbage Collection

垃圾收集有优点和缺点。这意味着手动管理对象的生命周期并不重要。但是，这也意味着JavaScript引擎可能会在应用程序开发人员无法控制的情况下启动可能持久的操作。除非应用程序开发人员仔细考虑JavaScript堆使用情况，否则垃圾收集的频率和持续时间可能会对应用程序体验产生负面影响。



##### Manually Invoking the Garbage Collector

用QML编写的应用程序（很可能）需要在某个阶段执行垃圾收集。虽然当可用空闲内存量较少时，JavaScript引擎会自动触发垃圾收集，但如果应用程序开发人员决定何时手动调用垃圾收集器，则有时会更好（尽管通常情况并非如此）。

应用程序开发人员可能最了解应用程序何时会在相当长的一段时间内处于空闲状态。如果QML应用程序使用大量JavaScript堆内存，在特别对性能敏感的任务（例如，列表滚动，动画等）期间导致定期和破坏性的垃圾收集周期，则应用程序开发人员可以很好地手动调用零活动期间的垃圾收集器。空闲时段是执行垃圾收集的理想选择，因为用户不会注意到在活动发生时调用垃圾收集器会导致用户体验（跳过的帧，生涩的动画等）的任何降级。

可以通过`gc()`在JavaScript中调用来手动调用垃圾收集器。这将导致执行全面的收集周期，这可能需要几百到几千毫秒才能完成，因此如果可能的话应该避免。



#### Memory vs Performance Trade-offs

在某些情况下，可以权衡增加的内存使用量以减少处理时间。例如，将紧密循环中使用的符号查找结果缓存到JavaScript表达式中的临时变量将在评估该表达式时显着提高性能，但它涉及分配临时变量。在某些情况下，这些权衡是明智的（例如上面的情况，这几乎总是合理的），但在其他情况下，允许处理稍长一些可能会更好，以避免增加系统的内存压力。

在某些情况下，内存压力增加的影响可能是极端的。在某些情况下，为假定的性能增益而牺牲内存使用量会导致页面抖动或缓存捶打增加，从而导致性能大幅下降。始终需要仔细权衡权衡取舍的影响，以确定在特定情况下哪种解决方案最佳。

有关缓存性能和内存时间权衡的深入信息，请参阅Ulrich Drepper的优秀文章“每个程序员应该了解内存的内容”（可从http://ftp.linux.org.ua/pub/docs/获取）截至2012年4月18日的developer / general / cpumemory.pdf），有关C ++特定优化的信息，请参阅Agner Fog关于优化C ++应用程序的优秀手册（可在http://www.agner.org/optimize/获取，如下所示） 2012年4月18日）。