# Item Positioners



定位器项是管理声明性用户界面中项的位置的容器项。定位器的行为方式与标准Qt小部件使用的[布局管理器](https://doc.qt.io/qt-5/qtwidgets-index.html)类似，只是它们本身也是容器。

当需要以常规布局排列时，定位器可以更轻松地处理许多项目。

[Qt Quick Layouts](https://doc.qt.io/qt-5/qtquicklayouts-index.html)也可用于在用户界面中安排Qt Quick项目。它们在声明性用户界面上管理项目的位置和大小，非常适合可调整大小的用户界面。



## Positioners

Qt Quick图形类型的基本集中提供了一组标准定位器：

|                                                              |                                                              |
| ------------------------------------------------------------ | ------------------------------------------------------------ |
| [Column](https://doc.qt.io/qt-5/qml-qtquick-column.html)     | Positions its children in a column                           |
| [Flow](https://doc.qt.io/qt-5/qml-qtquick-flow.html)         | Positions its children side by side, wrapping as necessary   |
| [Grid](https://doc.qt.io/qt-5/qml-qtquick-grid.html)         | Positions its children in grid formation                     |
| [LayoutMirroring](https://doc.qt.io/qt-5/qml-qtquick-layoutmirroring.html) | Property used to mirror layout behavior                      |
| [Positioner](https://doc.qt.io/qt-5/qml-qtquick-positioner.html) | Provides attached properties that contain details on where an item exists in a positioner |
| [Row](https://doc.qt.io/qt-5/qml-qtquick-row.html)           | Positions its children in a row                              |



### Column Items

![img](组件定位.assets/qml-column.png)

[Column](https://doc.qt.io/qt-5/qml-qtquick-column.html) items are used to vertically arrange items. The following example uses a Column item to arrange three [Rectangle](https://doc.qt.io/qt-5/qml-qtquick-rectangle.html) items in an area defined by an outer [Item](https://doc.qt.io/qt-5/qml-qtquick-item.html). The [spacing](https://doc.qt.io/qt-5/qml-qtquick-column.html#spacing-prop) property is set to include a small amount of space between the rectangles.

```
import QtQuick 2.0

Item {
    width: 310; height: 170

    Column {
        anchors.horizontalCenter: parent.horizontalCenter
        anchors.verticalCenter: parent.verticalCenter

        spacing: 5

        Rectangle { color: "lightblue"; radius: 10.0
                    width: 300; height: 50
                    Text { anchors.centerIn: parent
                           font.pointSize: 24; text: "Books" } }
        Rectangle { color: "gold"; radius: 10.0
                    width: 300; height: 50
                    Text { anchors.centerIn: parent
                           font.pointSize: 24; text: "Music" } }
        Rectangle { color: "lightgreen"; radius: 10.0
                    width: 300; height: 50
                    Text { anchors.centerIn: parent
                           font.pointSize: 24; text: "Movies" } }
    }
}
```

请注意，由于Column直接从Item继承，因此如果需要，必须将任何背景颜色添加到父Rectangle。



### Row Items

![img](组件定位.assets/qml-row.png)

[Row](https://doc.qt.io/qt-5/qml-qtquick-row.html) items are used to horizontally arrange items. The following example uses a Row item to arrange three rounded [Rectangle](https://doc.qt.io/qt-5/qml-qtquick-rectangle.html) items in an area defined by an outer colored Rectangle. The [spacing](https://doc.qt.io/qt-5/qml-qtquick-row.html#spacing-prop) property is set to include a small amount of space between the rectangles.

We ensure that the parent Rectangle is large enough so that there is some space left around the edges of the horizontally centered Row item.

```
import QtQuick 2.0

Rectangle {
    width: 320; height: 110
    color: "#c0c0c0"

    Row {
        anchors.horizontalCenter: parent.horizontalCenter
        anchors.verticalCenter: parent.verticalCenter

        spacing: 5

        Rectangle { width: 100; height: 100; radius: 20.0
                    color: "#024c1c" }
        Rectangle { width: 100; height: 100; radius: 20.0
                    color: "#42a51c" }
        Rectangle { width: 100; height: 100; radius: 20.0
                    color: "white" }
    }
}
```



### Grid Items

![img](组件定位.assets/qml-grid-spacing.png)

[Grid](https://doc.qt.io/qt-5/qml-qtquick-grid.html) items are used to place items in a grid or table arrangement. The following example uses a Grid item to place four [Rectangle](https://doc.qt.io/qt-5/qml-qtquick-rectangle.html) items in a 2-by-2 grid. As with the other positioners, the spacing between items can be specified using the [spacing](https://doc.qt.io/qt-5/qml-qtquick-grid.html#spacing-prop) property.

```
import QtQuick 2.0

Rectangle {
    width: 112; height: 112
    color: "#303030"

    Grid {
        anchors.horizontalCenter: parent.horizontalCenter
        anchors.verticalCenter: parent.verticalCenter
        columns: 2
        spacing: 6

        Rectangle { color: "#aa6666"; width: 50; height: 50 }
        Rectangle { color: "#aaaa66"; width: 50; height: 50 }
        Rectangle { color: "#9999aa"; width: 50; height: 50 }
        Rectangle { color: "#6666aa"; width: 50; height: 50 }
    }
}
```

在项目之间插入的水平和垂直间距之间没有区别，因此必须在项目本身内添加任何额外的空间。

必须通过在网格定义中的适当位置定义占位符项来创建网格中的任何空单元格。



### Flow Items

![img](组件定位.assets/qml-flow-text1.png) ![img](组件定位.assets/qml-flow-text2.png)

[Flow](https://doc.qt.io/qt-5/qml-qtquick-flow.html) 项目用于在页面上放置单词等项目，包含非重叠项目的行或列。

流项目以与[网格](https://doc.qt.io/qt-5/qml-qtquick-grid.html)项目类似的方式排列项目，项目沿一个轴（短轴）排列成行，并且项目行沿另一个轴（主轴）彼此相邻放置。流动方向以及物品之间的间距由[流动](https://doc.qt.io/qt-5/qml-qtquick-flow.html#flow-prop)和[间隔](https://doc.qt.io/qt-5/qml-qtquick-flow.html#spacing-prop)属性控制。

以下示例显示包含许多[Text](https://doc.qt.io/qt-5/qml-qtquick-text.html)子项的Flow 项。这些以与屏幕截图中显示的方式类似的方式排列。


```
import QtQuick 2.0

Rectangle {
    color: "lightblue"
    width: 300; height: 200

    Flow {
        anchors.fill: parent
        anchors.margins: 4
        spacing: 10

        Text { text: "Text"; font.pixelSize: 40 }
        Text { text: "items"; font.pixelSize: 40 }
        Text { text: "flowing"; font.pixelSize: 40 }
        Text { text: "inside"; font.pixelSize: 40 }
        Text { text: "a"; font.pixelSize: 40 }
        Text { text: "Flow"; font.pixelSize: 40 }
        Text { text: "item"; font.pixelSize: 40 }
    }
}
```

Grid和Flow定位器之间的主要区别在于Flow内部的项目在辅助轴上空间不足时会换行，如果项目的大小不均匀，则一行上的项目可能无法与另一行上的项目对齐。与网格项目一样，项目之间和项目行之间没有独立的间距控制。



## Other Ways to Position Items

There are several other ways to position items in a user interface. In addition to the basic technique of specifying their coordinates directly, they can be positioned relative to other items with [anchors](https://doc.qt.io/qt-5/qtquick-positioning-anchors.html), or used with [QML Data Models](https://doc.qt.io/qt-5/qtquick-modelviewsdata-modelview.html#qml-data-models) such as [Object Model](https://doc.qt.io/qt-5/qtquick-modelviewsdata-modelview.html#object-model).