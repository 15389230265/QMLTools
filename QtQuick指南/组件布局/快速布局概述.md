# Qt Quick Layouts Overview



Qt快速布局是用于在用户界面中排列项目的项目。由于Qt Quick Layouts也调整了它们的项目，因此它们非常适合可调整大小的用户界面。



## Getting Started

可以使用`.qml`文件中的以下import语句将QML类型导入应用程序。

```
import QtQuick.Layouts 1.11
```



## Key Features

一些关键功能是：

- 可以使用[Layout.alignment](https://doc.qt.io/qt-5/qml-qtquick-layouts-layout.html#alignment-attached-prop)属性指定项目的[对齐](https://doc.qt.io/qt-5/qml-qtquick-layouts-layout.html#alignment-attached-prop)方式
- 可以使用[Layout.fillWidth](https://doc.qt.io/qt-5/qml-qtquick-layouts-layout.html#fillWidth-attached-prop)和[Layout.fillHeight](https://doc.qt.io/qt-5/qml-qtquick-layouts-layout.html#fillHeight-attached-prop)属性指定[可调整大小的项目](https://doc.qt.io/qt-5/qml-qtquick-layouts-layout.html#fillWidth-attached-prop)。
- 可以使用[Layout.minimumWidth](https://doc.qt.io/qt-5/qml-qtquick-layouts-layout.html#minimumWidth-attached-prop)，[Layout.preferredWidth](https://doc.qt.io/qt-5/qml-qtquick-layouts-layout.html#preferredWidth-attached-prop)和[Layout.maximumWidth](https://doc.qt.io/qt-5/qml-qtquick-layouts-layout.html#maximumWidth-attached-prop)属性指定[大小约束](https://doc.qt.io/qt-5/qtquicklayouts-overview.html#size-constraints)（“宽度”可以替换为“高度”，以指定与高度类似的约束）。
- 可以使用[spacing](https://doc.qt.io/qt-5/qml-qtquick-layouts-rowlayout.html#spacing-prop)，[rowSpacing](https://doc.qt.io/qt-5/qml-qtquick-layouts-gridlayout.html#rowSpacing-prop)或[columnSpacing](https://doc.qt.io/qt-5/qml-qtquick-layouts-gridlayout.html#columnSpacing-prop)指定[Spacings](https://doc.qt.io/qt-5/qml-qtquick-layouts-rowlayout.html#spacing-prop)

除了上述功能外，[GridLayout](https://doc.qt.io/qt-5/qml-qtquick-layouts-gridlayout.html)还添加了以下功能：

- 可以使用[Layout.row](https://doc.qt.io/qt-5/qml-qtquick-layouts-layout.html#row-attached-prop)和[Layout.column](https://doc.qt.io/qt-5/qml-qtquick-layouts-layout.html#column-attached-prop)属性指定[网格坐标](https://doc.qt.io/qt-5/qml-qtquick-layouts-layout.html#row-attached-prop)。
- [自动网格坐标](https://doc.qt.io/qt-5/qml-qtquick-layouts-gridlayout.html#flow-prop)与[flow](https://doc.qt.io/qt-5/qml-qtquick-layouts-gridlayout.html#flow-prop)，[rows](https://doc.qt.io/qt-5/qml-qtquick-layouts-gridlayout.html#rows-prop)和[columns](https://doc.qt.io/qt-5/qml-qtquick-layouts-gridlayout.html#columns-prop)属性一起使用。
- [跨度](https://doc.qt.io/qt-5/qml-qtquick-layouts-layout.html#columnSpan-attached-prop)横跨行或列可以与指定[Layout.rowSpan](https://doc.qt.io/qt-5/qml-qtquick-layouts-layout.html#rowSpan-attached-prop)和[Layout.columnSpan](https://doc.qt.io/qt-5/qml-qtquick-layouts-layout.html#columnSpan-attached-prop)性质。



## A Simple Layout

```
Window {
    RowLayout {
        anchors.fill: parent
        spacing: 6
        Rectangle {
            color: 'azure'
            Layout.preferredWidth: 100
            Layout.preferredHeight: 150
        }
        Rectangle {
            color: "plum"
            Layout.fillWidth: true
            Layout.fillHeight: true
        }
    }
}
```

由于使用布局的目的是在布局更改大小时重新排列其子项，应用程序应确保布局调整大小。在上面的代码片段中，[RowLayout](https://doc.qt.io/qt-5/qml-qtquick-layouts-rowlayout.html)通过指定来确保`anchors.fill: parent`。但是，它也可以通过其他方式，例如直接指定[宽度](https://doc.qt.io/qt-5/qml-qtquick-item.html#width-prop)和[高度](https://doc.qt.io/qt-5/qml-qtquick-item.html#height-prop)属性。在同一个片段中，`azure`Rectangle具有固定大小的`(100, 150)`像素，并且`plum`Rectangle将展开以占据它分配的所有空间。

**注意：**布局负责其子项的几何。因此，您不应指定[宽度](https://doc.qt.io/qt-5/qml-qtquick-item.html#width-prop)，[高度](https://doc.qt.io/qt-5/qml-qtquick-item.html#height-prop)，[x](https://doc.qt.io/qt-5/qml-qtquick-item.html#x-prop)，[y](https://doc.qt.io/qt-5/qml-qtquick-item.html#y-prop)或可能影响这些项目上的属性（如[锚点](https://doc.qt.io/qt-5/qml-qtquick-item.html#anchors-prop)）的任何其他属性。否则会产生利益冲突，结果不明确。如果子项是布局，也是这种情况。因此，只有没有父布局的布局才能有`anchors.fill: parent`。

布局中的所有项目之间都有6个像素的间距：

```
spacing: 6
```



### Size Constraints

由于项目可以通过其布局调整大小，因此布局需要知道[Layout.fillWidth](https://doc.qt.io/qt-5/qml-qtquick-layouts-layout.html#fillWidth-attached-prop)或[Layout.fillHeight](https://doc.qt.io/qt-5/qml-qtquick-layouts-layout.html#fillHeight-attached-prop)设置为的所有项目的[最小](https://doc.qt.io/qt-5/qml-qtquick-layouts-layout.html#minimumWidth-attached-prop)，[首选](https://doc.qt.io/qt-5/qml-qtquick-layouts-layout.html#preferredWidth-attached-prop)和[最大](https://doc.qt.io/qt-5/qml-qtquick-layouts-layout.html#maximumWidth-attached-prop)大小。例如，下面将生成一个布局，其中两个并排放置的矩形水平拉伸。天蓝色矩形可以从50x150调整到300x150，梅花矩形可以从100x100调整到∞x100。

```
RowLayout {
    id: layout
    anchors.fill: parent
    spacing: 6
    Rectangle {
        color: 'azure'
        Layout.fillWidth: true
        Layout.minimumWidth: 50
        Layout.preferredWidth: 100
        Layout.maximumWidth: 300
        Layout.minimumHeight: 150
        Text {
            anchors.centerIn: parent
            text: parent.width + 'x' + parent.height
        }
    }
    Rectangle {
        color: 'plum'
        Layout.fillWidth: true
        Layout.minimumWidth: 100
        Layout.preferredWidth: 200
        Layout.preferredHeight: 100
        Text {
            anchors.centerIn: parent
            text: parent.width + 'x' + parent.height
        }
    }
}
```

!["RowLayout at its minimum"](快速布局概述.assets/rowlayout-minimum.png)

组合每个项的约束将为布局元素提供这些隐式约束：

|                                | minimum | preferred | maximum                        |
| ------------------------------ | ------- | --------- | ------------------------------ |
| implicit constraints (width)   | 156     | 306       | ∞ (`Number.POSITIVE_INFINITY`) |
| implicit constraints (heights) | 150     | 150       | 150                            |

因此，在不破坏其子物品的任何约束的情况下，布局不能比156窄或者比150更高或更短。



### Specifying Preferred Size

对于每个项目，有效的首选大小可能来自几个候选属性之一。为了确定有效的首选大小，它将按以下顺序查询这些候选属性，并使用具有有效宽度或高度的第一个候选。

| Candidate properties                                         | Description                                                  |
| ------------------------------------------------------------ | ------------------------------------------------------------ |
| [Layout.preferredWidth](https://doc.qt.io/qt-5/qml-qtquick-layouts-layout.html#preferredWidth-attached-prop)or [Layout.preferredHeight](https://doc.qt.io/qt-5/qml-qtquick-layouts-layout.html#preferredHeight-attached-prop) | 如果默认隐式大小未给出最佳排列，则应用程序应修改这些属性。   |
| [implicitWidth](https://doc.qt.io/qt-5/qml-qtquick-item.html#implicitWidth-prop) or [implicitHeight](https://doc.qt.io/qt-5/qml-qtquick-item.html#implicitHeight-prop) | 这些属性应该由每个项目提供，以提供有意义的理想大小，例如显示[Text](https://doc.qt.io/qt-5/qml-qtquick-text.html)类型的所有内容所需的大小。隐式宽度或高度`0`被解释为无效。 |
| [width](https://doc.qt.io/qt-5/qml-qtquick-item.html#width-prop) and [height](https://doc.qt.io/qt-5/qml-qtquick-item.html#height-prop) | 如果上述属性均无效，则布局将采用[width](https://doc.qt.io/qt-5/qml-qtquick-item.html#width-prop)和[height](https://doc.qt.io/qt-5/qml-qtquick-item.html#height-prop)属性。 |

项目可以指定[Layout.preferredWidth，](https://doc.qt.io/qt-5/qml-qtquick-layouts-layout.html#preferredWidth-attached-prop)而无需指定[Layout.preferredHeight](https://doc.qt.io/qt-5/qml-qtquick-layouts-layout.html#preferredHeight-attached-prop)。在这种情况下，有效的首选高度将由[implicitHeight](https://doc.qt.io/qt-5/qml-qtquick-item.html#implicitHeight-prop)（或最终的[高度](https://doc.qt.io/qt-5/qml-qtquick-item.html#height-prop)）确定。

**注意：**使用[宽度](https://doc.qt.io/qt-5/qml-qtquick-item.html#width-prop)或[高度](https://doc.qt.io/qt-5/qml-qtquick-item.html#height-prop)属性仅作为最终后备。如果要覆盖首选大小，建议使用[Layout.preferredWidth](https://doc.qt.io/qt-5/qml-qtquick-layouts-layout.html#preferredWidth-attached-prop)或[Layout.preferredHeight](https://doc.qt.io/qt-5/qml-qtquick-layouts-layout.html#preferredHeight-attached-prop)。依赖于[width](https://doc.qt.io/qt-5/qml-qtquick-item.html#width-prop)或[height](https://doc.qt.io/qt-5/qml-qtquick-item.html#height-prop)属性来指定首选大小可能会产生一些意外行为。例如，更改[宽度](https://doc.qt.io/qt-5/qml-qtquick-item.html#width-prop)或[高度](https://doc.qt.io/qt-5/qml-qtquick-item.html#height-prop)属性不会触发布局重新排列。此外，当强制布局进行完全重建时，它可能使用实际的宽度和高度，而不是QML文件中指定的宽度和高度。



## Connecting Windows and Layouts

您可以使用常规锚定概念来确保布局将遵循窗口大小调整。

```
RowLayout {
    id: layout
    anchors.fill: parent
```

布局的大小限制可用于确保窗口无法在布局约束之外调整大小。您可以从布局中获取大小约束，并将这些约束设置为Window元素的minimumWidth，minimumHeight，maximumWidth和maximumHeight。以下代码确保窗口无法在布局约束之外调整大小：

```
minimumWidth: layout.Layout.minimumWidth
minimumHeight: layout.Layout.minimumHeight
maximumWidth: 1000
maximumHeight: layout.Layout.maximumHeight
```

**注意：**由于layout.Layout.maximumWidth在这种情况下是无限的，我们不能将它绑定到Window的maximumWidth属性，因为这是一个整数。因此，我们将固定的最大宽度设置为1000。

最后，您通常希望窗口的初始大小为布局的隐式大小：

```
width: layout.implicitWidth
height: layout.implicitHeight
```